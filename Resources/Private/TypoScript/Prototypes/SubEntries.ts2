prototype(PS:SubEntries) < prototype(Fusion:Tag) {
	@if.hasSubEntries = ${q(node).children(Configuration.setting('Dotpulse.PageSection.mixin')).count() > 0}

	attributes = Fusion:Attributes {
		id = PS:CollectionProperty {
			seperator {
				first = ''
				default = ${configuration.idSeperator}
			}
			@process.sub = ${value + '-sub'}
		}

		class = Fusion:RawArray {
			wrapper = 'pagesection-subentries-wrapper'
			hasSubEntries = ${'pagesection-subentries-' + (q(node).children(Configuration.setting('Dotpulse.PageSection.mixin')).count() > 1 ? 'has-menu' : 'no-menu')}
		}
	}

	content = Fusion:Array {
		menu = PS:Menu {
			level = 'subentry'
		}

		content = PS:Collection {
			renderSubEntries = false
			attributes {
				id >
				class.wrapper = 'pagesection-subentry-wrapper'
			}
			content.itemRenderer {
				isSubentry = true
			}
		}
	}
}
