prototype(PS:Menu) < prototype(Fusion:Tag) {
	@context {
		configuration = ${Configuration.setting('Dotpulse.PageSection')}
		nodeObject = ${this.nodeObject}
		level = ${this.level}
	}
	nodeObject = ${node}
	level = false
	@if.hasMoreThanOneChildren = ${q(nodeObject).children(configuration.mixin).count() > 1}

	tagName = 'ul'
	attributes {
		class = Fusion:RawArray {
			default = ${'pagesection-' + (level ? level + '-' : '') + 'menu'}
		}
	}
	content = Fusion:Collection {
		collection = ${q(nodeObject).children(configuration.mixin)}
		itemName = 'node'
		iterationName = 'nodeIterator'
		itemRenderer = Fusion:Tag {
			tagName = 'li'
			attributes {
				class = Fusion:RawArray {
					active = ${nodeIterator.isFirst ? 'active' : false}
					default = ${'pagesection-' + (level ? level + '-' : '') + 'item'}
				}
			}
			content = Fusion:Tag {
				tagName = 'a'
				attributes {
					class = Fusion:RawArray {
						default = ${'pagesection-' + (level ? level + '-' : '') + 'menu-link'}
					}
					href = PS:CollectionProperty {
						@context.nodeObject = ${item}
						seperator {
							first = '#'
							default = ${configuration.idSeperator}
						}
					}
				}
				content = ${q(node).property('title')}
			}
		}
	}
}
