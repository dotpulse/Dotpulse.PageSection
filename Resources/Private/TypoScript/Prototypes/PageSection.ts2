prototype(PS:PageSection) < prototype(Fusion:Tag) {
	@if.render = ${q(node).children(configuration.mixin).count() ? true : false}
	@context.configuration = ${Configuration.setting('Dotpulse.PageSection')}

	attributes = Fusion:Attributes {
		id = 'pagesection'
		class = Fusion:RawArray {
			wrapper = 'pagesection-container'
		}
	}

	content = Fusion:Array {
		menu = PS:Menu
		content = Fusion:Collection {
			collection = ${q(node).children(configuration.mixin)}
			itemName = 'node'
			iterationName = 'nodeIterator'
			itemRenderer = PS:Collection
		}
	}

	@cache {
		mode = 'cached'

		entryIdentifier {
			node = ${node}
			editPreviewMode = ${node.context.currentRenderingMode.name}
		}

		entryTags {
			1 = ${'Node_' + documentNode.identifier}
			2 = ${'DescendantOf_' + documentNode.identifier}
		}
	}
}
